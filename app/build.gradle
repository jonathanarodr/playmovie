plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        applicationId "br.com.jonathanarodr.playmovie"

        versionCode 2
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String" , "SERVER_URL", '"https://api.themoviedb.org"')
        buildConfigField("String" , "AUTHORIZATION_KEY", '"[my-authorization-key"')
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    sourceSets {
        main {
            java.srcDir("src/main/kotlin")
        }
        test {
            java.srcDir("src/test/kotlin")
            java.srcDir("src/sharedTest/kotlin")
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = java_version
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests {
            returnDefaultValues true
            includeAndroidResources true
        }
    }
    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version")

    implementation("androidx.core:core-ktx:$android_core_version")

    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-common-java8:$lifecycle_version")

    implementation("androidx.room:room-ktx:$room_version")
    implementation("androidx.room:room-runtime:$room_version")
    kapt("androidx.room:room-compiler:$room_version")

    implementation("androidx.navigation:navigation-fragment-ktx:$navigation_version")
    implementation("androidx.navigation:navigation-ui-ktx:$navigation_version")

    implementation("androidx.appcompat:appcompat:${appcompat_version}")
    implementation("androidx.recyclerview:recyclerview:$recycler_version")
    implementation("androidx.constraintlayout:constraintlayout:$constraint_version")
    implementation("androidx.cardview:cardview:$cardview_version")
    implementation("androidx.browser:browser:$browser_version")
    implementation("com.google.android.material:material:$material_version")
    implementation ("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    kapt("com.github.bumptech.glide:compiler:$glide_version")

    implementation("com.google.code.gson:gson:$gson_version")

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation("io.insert-koin:koin-android:$koin_version")

    implementation("com.squareup.retrofit2:retrofit:$retrofit_version")
    implementation("com.squareup.retrofit2:converter-gson:$retrofit_version")

    implementation(platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    testImplementation("junit:junit:$junit_version")
    testImplementation("io.mockk:mockk:$mockk_version")

    androidTestUtil("androidx.test:orchestrator:$android_test_version")
    androidTestImplementation("androidx.test:runner:$android_test_version")
    androidTestImplementation("androidx.test:rules:$android_test_version")
    androidTestImplementation("androidx.test.espresso:espresso-core:$espresso_version") {
        exclude group: "com.android.support", module: "support-annotations"
    }
    androidTestImplementation("io.mockk:mockk-android:$mockk_version")
}